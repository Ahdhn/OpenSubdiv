#!/bin/bash
gnuplot << EOF

# $1 output file
# $2 data file
# $3 model name

load "common.gpi"
set output "$1"

set border 3 front linetype -1 linewidth 1.000
set boxwidth 0.95 absolute
set style fill solid 1.00 noborder

set grid noxtics ytics

set key bmargin center horizontal Left reverse noenhanced autotitles columnhead nobox
set key samplen 2 width -2 maxrows 2

set yrange [0:2.5]
set style histogram clustered gap 1 title offset character 2, 0.25, 0

set style data histograms
set xtics border in scale 0,0 nomirror rotate by -45  offset character 0, 0, 0 autojustify
set xtics norangelimit font ",8"
set xtics ()
set ytics border in scale 0,0 mirror norotate offset character 0, 0, 0 autojustify

set title "Relative Performance on \\\`$3' Model"
set ylabel "Normalized Performance"

plot \
    newhistogram "CPU Kernels", \
        "$2" using 2:xtic(1) t col ls 1, \
        "$2" using 3         t col ls 2, \
        "$2" using 4         t col ls 3, \
        "$2" using 5         t col ls 4, \
    newhistogram "GPU Kernels", \
        "$2" using 6:xtic(1) t col ls 5, \
        "$2" using 7         t col ls 6, \
        "$2" using 8         t col ls 7, \
        "$2" using 9         t col ls 8

EOF
