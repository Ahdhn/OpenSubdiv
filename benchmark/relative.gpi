#!/bin/bash
gnuplot << EOF

# $1 output file
# $2 data file
# $3 model name

load "common.gpi"
set output "$1"

set key bottom center outside maxrows 2
set key autotitle columnhead
#set key samplen 2 width -2
set key width -2

set style data histograms
set style histogram clustered gap 1
set style fill solid 1.00 noborder

set grid noxtics ytics

set yrange [0:]
set autoscale ymax
set ytics 0.5

set xtics nomirror rotate by -45
set ytics mirror norotate

set title "Relative Performance on \\\`$3' Model"
set ylabel "Normalized Performance" offset 1.5
set format y "%g\$\\\\times\$"

plot \
    newhistogram "CPU Kernels", \
        "$2" using 2:xtic(1) t col ls 1, \
        "$2" using 6         t col ls 3, \
        "$2" using 3         t col ls 2, \
        "$2" using 8         t col ls 4, \
    newhistogram "GPU Kernels", \
        "$2" using 5:xtic(1) t col ls 6, \
        "$2" using 7         t col ls 7, \
        "$2" using 4         t col ls 5, \
        "$2" using 9         t col ls 8

EOF
